#!/usr/bin/perl
use strict;
use warnings;
use autodie;
use File::Basename;

# Usage:
#   perl compat.pl -- plugin/compat.ml src/ExtractionQCCompat.v
#   perl compat.pl -- compat.ml
#   perl compat.pl -- ExtractionQCCompat.v
#
# Generate the given files depending on their basenames.
# That makes this script flexible to the exact location of those files.

my $coq_version = `coqc -print-version`;
$coq_version =~ s/([\d.]+).*/$1/;

if ($coq_version lt '8.12' || '8.14' le $coq_version) {
  print STDERR "Warning: This version of Coq is not supported: $coq_version";
  print STDERR "Currently supported versions of Coq: 8.13, 8.12.\n"
}

sub writefile {
  my ($filename, $contents) = @_;
  open(my $file, '>', $filename);
  print $file $contents;
  close $file;
}

# Generate file plugin/compat.ml
sub compat_ml {

  my $compat_ml = '(* THIS FILE IS GENERATED BY compat.pl *)
let number (x : NumTok.Signed.t) : Constrexpr.prim_token = Constrexpr.Number x
let from_Number : Constrexpr.prim_token -> NumTok.Signed.t option = function
  | Constrexpr.Number x -> Some x
  | _ -> None
';

  # Since 8.13: Constrexpr.Number
  # Before:     Constrexpr.Numeral
  if ($coq_version lt '8.13') {
    $compat_ml =~ s/Constrexpr\.Number/Constrexpr.Numeral/g;
  }

  return $compat_ml;
}

# Generate file src/ExtractionQCCompat.ml
sub extractionqccompat_v {

  my $extractionqccompat_v = '(* THIS FILE IS GENERATED BY compat.pl *)
From Coq Require Import Extraction.

Extract Inductive Number.int => "((Obj.t -> Obj.t) -> (Obj.t -> Obj.t) -> Obj.t) (* __int *)"
  [ "(fun x dec _ -> dec (Obj.magic x))"
    "(fun y _ hex -> hex (Obj.magic y))"
  ] "(fun i dec hex -> Obj.magic i dec hex)".
';

  # Since 8.13: Number.int
  # Before:     Numeral.int
  if ($coq_version lt '8.13') {
    $extractionqccompat_v =~ s/Number\.int/Numeral.int/g;
  }

  return $extractionqccompat_v;
}

for my $filename (@ARGV) {
  my $bn = basename($filename);

  if ($bn eq 'compat.ml') {
    writefile($filename, compat_ml());
  } elsif ($bn eq 'ExtractionQCCompat.v') {
    writefile($filename, extractionqccompat_v());
  } else {
    print STDERR "Warning: Unrecognized file name $filename\n";
  }
}
